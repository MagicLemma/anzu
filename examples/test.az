
struct vector!(T)
{
    arr: arena&;
    data: T[];
    size: u64;

    fn to_span(self: vector!(T)&) -> T[]
    {
        return self.data[0u : self.size];
    }

    fn reserve(self: vector!(T)&, size: u64) -> null
    {
        let cap := self.capacity();
        if cap < size {
            var new_buf := new(self.arr, size) T();
            
            # Copy existing values over
            {
                var idx := 0u;
                while idx < self.size {
                    new_buf[idx] = self.data[idx];
                    idx = idx + 1u;
                }
            }
            self.data = new_buf;
        }
    }

    fn capacity(self: vector!(T) const&) -> u64
    {
        return self.data.size();
    }

    fn at(self: vector!(T) const&, index: u64) -> T
    {
        return self.data[index];
    }

    fn push(self: vector!(T)&, value: T) -> null
    {
        let cap := self.capacity();
        if self.size == cap {
            var new_cap := 8u;
            if cap > 0u {
                new_cap = cap * 2u;
            }
            self.reserve(new_cap);
        }
        self.data[self.size] = value;
        self.size = self.size + 1u;
    }
}

fn new_vector!(T)(arr: arena&) -> vector!(T)
{
    return vector!(T)(arr, nullptr, 0u);
}


arena a;
var v := new_vector!(i64)(a&);
v.reserve(25u);
var idx := 0;
while idx < 20 {
    v.push(idx);
    idx = idx + 1;
}

for a in v.to_span() {
    print("{}\n", a@);
}
print("{} {} {}\n", v.capacity(), v.size, a.size());